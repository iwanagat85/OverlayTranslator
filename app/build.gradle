apply plugin: 'com.android.application'

// Manifest version
def versionMajor = 1
def versionMinor = 3
def versionPatch = 2

def packageName = 'jp.iwanagat85.overlaytranslator'
def appName = 'OverlayTranslator'

android {
    compileSdkVersion compile_sdk_version as int
    buildToolsVersion build_tools_version

    defaultConfig {
        applicationId packageName
        minSdkVersion min_sdk_version as int
        targetSdkVersion target_sdk_version as int
        versionCode versionMajor * 10000 + versionMinor * 100 + versionPatch
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    applicationVariants.all { variant ->
        variant.resValue "string", "versionInfo", variant.versionName
    }

    signingConfigs {
        debug {
            storeFile rootProject.file('debug.keystore')
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }

        release {
            storeFile rootProject.file('release.keystore')
            storePassword System.getenv('ANDROID_KEYSTORE_PASSWORD')
            keyAlias System.getenv('ANDROID_KEYSTORE_ALIAS')
            keyPassword System.getenv('ANDROID_KEYSTORE_PRIVATE_KEY_PASSWORD')
        }
    }

    buildTypes {
        debug {
            manifestPlaceholders = [scheme: 'overlaytranslator', host: 'iwanagat85.jp.debug']
            resValue 'string', 'app_name', "${appName} Debug"
            signingConfig signingConfigs.debug
            debuggable true
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix '_DEBUG'

            buildConfigField 'String', 'CLOUD_VISION_API_KEY', "\"${CLOUD_VISION_API_KEY}\""
            buildConfigField 'String', 'GAS_TRANSLATOR_API_KEY', "\"${GAS_TRANSLATOR_API_KEY}\""
        }

        release {
            manifestPlaceholders = [scheme: 'overlaytranslator', host: 'iwanagat85.jp']
            resValue 'string', 'app_name', appName
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'CLOUD_VISION_API_KEY', "\"${CLOUD_VISION_API_KEY}\""
            buildConfigField 'String', 'GAS_TRANSLATOR_API_KEY', "\"${GAS_TRANSLATOR_API_KEY}\""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:support-v4:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support:preference-v14:27.1.1'

    // Network
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.10.0'

    // Structure
    implementation 'io.reactivex.rxjava2:rxjava:2.1.12'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    implementation 'com.google.dagger:dagger:2.15'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.15'
    implementation 'com.google.dagger:dagger-android:2.15'
    implementation 'com.google.dagger:dagger-android-support:2.15'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.15'

    // Google API
    implementation 'com.google.api-client:google-api-client-android:1.22.0'
    implementation 'com.google.apis:google-api-services-vision:v1-rev357-1.22.0'

    // Test
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}